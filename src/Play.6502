; Play - This programme contains the in-game logic

INCLUDE "ChuckieOS.6502"

ORG mini_prog_start_address

.update_harry
{
    LDA #&00
    STA harry_dx
    STA harry_dy
    LDA key_press_bits
    LSR A
    BCC not_right_key
    INC harry_dx
    .not_right_key
    LSR A
    BCC not_left_key
    DEC harry_dx
    .not_left_key
    LSR A
    BCC not_down_key
    DEC harry_dy
    .not_down_key
    LSR A
    BCC not_up_key
    INC harry_dy
    .not_up_key
    ASL harry_dy
    LDA harry_state
    BEQ state_zero
    CMP #&02
    BNE state_not_two
    JMP state_two_within_jump
    .state_not_two
    BCS state_greater_than_two
    JMP state_one_on_ladder
    .state_greater_than_two
    CMP #&03
    BNE state_greater_than_three
    JMP state_three_falling
    .state_greater_than_three
    JMP state_four_on_lift
    .state_zero
    LDA key_press_bits
    AND #&10
    BEQ not_jump_key
    JMP start_jump
    .not_jump_key
    LDA harry_dy
    BEQ not_ladder
    LDX harry_cell_x
    CPX #&03
    BNE not_ladder
    LDA harry_dy
    BMI try_going_down
    LDX harry_board_x
    LDY harry_board_y
    INY
    INY
    JSR cos_board_read_with_range_check
    AND #&02
    BEQ not_ladder
    BNE do_ladder_start
    .try_going_down
    LDX harry_board_x
    LDY harry_board_y
    DEY
    JSR cos_board_read_with_range_check
    AND #&02
    BEQ not_ladder
    .do_ladder_start
    LDA #&00
    STA harry_dx
    LDA #&01
    STA harry_state
    JMP update_facing
    .not_ladder
    LDA #&00
    STA harry_dy
    LDA harry_cell_x
    CLC
    ADC harry_dx
    LDX harry_board_x
    CMP #&00
    BPL not_entering_left_cell
    DEX
    .not_entering_left_cell
    CMP #&08
    BMI not_entering_right_cell
    INX
    .not_entering_right_cell
    LDY harry_board_y
    DEY
    JSR cos_board_read_with_range_check
    AND #&01
    BNE cell_has_wall
    TAY
    LDX #&FF
    LDA harry_dx
    CLC
    ADC harry_cell_x
    AND #&07
    CMP #&04
    BCS falling_left
    LDX #&01
    INY
    .falling_left
    STX harry_fall_facing
    STY harry_fall_scaled_vy
    LDA #&03
    STA harry_state
    .cell_has_wall
    JSR test_horizontal_obstructions
    BCC update_facing
    LDA #&00
    STA harry_dx
    .update_facing
    LDA harry_dx
    BEQ skip_update_facing
    STA harry_facing
    .skip_update_facing
    JMP harry_apply_dx_dy
    .state_one_on_ladder
    LDA key_press_bits
    AND #&10
    BEQ not_jump_from_ladder
    JMP start_jump
    .not_jump_from_ladder
    LDA harry_dx
    BEQ not_sideways_from_ladder
    LDX harry_cell_y
    BNE not_sideways_from_ladder
    LDX harry_board_x
    LDY harry_board_y
    DEY
    JSR cos_board_read_with_range_check
    AND #&01
    BEQ not_sideways_from_ladder
    LDA #&00
    STA harry_dy
    LDA #&00
    STA harry_state
    JMP set_harry_facing_ladder
    .not_sideways_from_ladder
    LDA #&00
    STA harry_dx
    LDA harry_dy
    BEQ set_harry_facing_ladder
    LDA harry_cell_y
    BNE set_harry_facing_ladder
    LDA harry_dy
    BMI ladder_going_down
    LDX harry_board_x
    LDY harry_board_y
    INY
    INY
    JSR cos_board_read_with_range_check
    AND #&02
    BNE set_harry_facing_ladder
    STA harry_dy
    JMP set_harry_facing_ladder
    .ladder_going_down
    LDX harry_board_x
    LDY harry_board_y
    DEY
    JSR cos_board_read_with_range_check
    AND #&02
    BNE set_harry_facing_ladder
    STA harry_dy
    .set_harry_facing_ladder
    LDA #&00
    STA harry_facing
    JMP harry_apply_dx_dy
    .state_two_within_jump
    LDA harry_fall_facing
    STA harry_dx
    LDA harry_dy
    STA &89
    LDA harry_fall_scaled_vy
    LSR A
    LSR A
    CMP #&06
    BCC vy_in_range
    LDA #&06
    .vy_in_range
    EOR #&FF
    SEC
    ADC #&02
    STA harry_dy
    INC harry_fall_scaled_vy
    LDA harry_y
    CMP #&DC
    BCC not_bounce_off_top
    LDA #&FF
    STA harry_dy
    LDA #&0C
    STA harry_fall_scaled_vy
    JMP test_lift_collision
    .not_bounce_off_top
    LDA harry_cell_x
    CLC
    ADC harry_dx
    CMP #&03
    BNE not_grabbing_ladder
    LDA &89
    BEQ not_grabbing_ladder
    BMI test_grab_ladder_with_down_key
    .comment_jumping_up_in_middle_of_cell
    LDX harry_board_x
    LDY harry_board_y
    INY
    JSR cos_board_read_with_range_check
    AND #&02
    BNE grab_ladder_with_up_key
    LDX harry_board_x
    LDY harry_board_y
    INY
    LDA harry_cell_y
    CMP #&04
    BCC harry_in_bottom_half_of_cell_2
    INY
    .harry_in_bottom_half_of_cell_2
    JSR cos_board_read_with_range_check
    AND #&02
    BEQ not_grabbing_ladder
    .grab_ladder_with_up_key
    LDA #&01
    STA harry_state
    LDA harry_cell_y
    CLC
    ADC harry_dy
    AND #&01
    BEQ up_grab_position_already_even
    INC harry_dy
    .up_grab_position_already_even
    JMP no_horizontal_bounce
    .test_grab_ladder_with_down_key
    LDX harry_board_x
    LDY harry_board_y
    JSR cos_board_read_with_range_check
    AND #&02
    BEQ not_grabbing_ladder
    LDX harry_board_x
    LDY harry_board_y
    INY
    JSR cos_board_read_with_range_check
    AND #&02
    BEQ not_grabbing_ladder
    LDA #&01
    STA harry_state
    LDA harry_cell_y
    CLC
    ADC harry_dy
    AND #&01
    BEQ down_grab_position_already_even
    DEC harry_dy
    .down_grab_position_already_even
    JMP no_horizontal_bounce
    .not_grabbing_ladder
    LDA harry_dy
    CLC
    ADC harry_cell_y
    BEQ test_jump_landing_on_wall
    BPL test_hop_up
    LDX harry_board_x
    LDY harry_board_y
    DEY
    JSR cos_board_read_with_range_check
    AND #&01
    BEQ test_lift_collision
    LDA #&00
    STA harry_state
    LDA #&00
    SEC
    SBC harry_cell_y
    STA harry_dy
    JMP test_lift_collision
    .test_jump_landing_on_wall
    LDX harry_board_x
    LDY harry_board_y
    DEY
    JSR cos_board_read_with_range_check
    AND #&01
    BEQ test_lift_collision
    LDA #&00
    STA harry_state
    JMP test_lift_collision
    .test_hop_up
    CMP #&08
    BNE test_lift_collision
    LDX harry_board_x
    LDY harry_board_y
    JSR cos_board_read_with_range_check
    AND #&01
    BEQ test_lift_collision
    LDA #&00
    STA harry_state
    JMP test_lift_collision
    .test_lift_collision
    LDA lift_flag
    BEQ no_lift
    LDA lift_x
    SEC
    SBC #&01
    CMP harry_x
    BCS no_lift
    ADC #&0A
    CMP harry_x
    BCC no_lift
    LDA harry_y
    SEC
    SBC #&11
    STA &8B
    SBC #&02
    CLC
    ADC harry_dy
    STA &8C
    LDA lift_y1
    CMP &8B
    BEQ join_lift_one
    BCS try_lift_two
    CMP &8C
    BCC try_lift_two
    .join_lift_one
    LDX lift_alternator
    BNE other_lift_moving
    CLC
    ADC #&01
    .other_lift_moving
    JMP join_a_lift
    .try_lift_two
    LDA lift_y2
    CMP &8B
    BEQ join_lift_two
    BCS no_lift
    CMP &8C
    BCC no_lift
    .join_lift_two
    LDX lift_alternator
    BEQ join_a_lift
    CLC
    ADC #&01
    .join_a_lift
    SEC
    SBC &8B
    CLC
    ADC #&01
    STA harry_dy
    LDA #&00
    STA harry_fall_scaled_vy
    LDA #&04
    STA harry_state
    JMP no_horizontal_bounce
    .no_lift
    JSR test_horizontal_obstructions
    BCC no_horizontal_bounce
    LDA #&00
    SEC
    SBC harry_dx
    STA harry_dx
    STA harry_fall_facing
    .no_horizontal_bounce
    JMP harry_apply_dx_dy
    .start_jump
    LDA #&00
    STA harry_fall_scaled_vy
    LDA #&02
    STA harry_state
    LDA harry_dx
    STA harry_fall_facing
    BEQ falling_straight_retain_facing
    STA harry_facing
    .falling_straight_retain_facing
    JMP state_two_within_jump
    .state_three_falling
    INC harry_fall_scaled_vy
    LDA harry_fall_scaled_vy
    CMP #&04
    BCS falling_faster
    LDA harry_fall_facing
    STA harry_dx
    LDA #&FF
    STA harry_dy
    JMP test_fall_landing
    .falling_faster
    LDA #&00
    STA harry_dx
    LDA harry_fall_scaled_vy
    LSR A
    LSR A
    CMP #&04
    BCC scaled_vy_in_range
    LDA #&03
    .scaled_vy_in_range
    EOR #&FF
    STA harry_dy
    .test_fall_landing
    LDA harry_dy
    CLC
    ADC harry_cell_y
    BEQ test_fall_landing_on_wall
    BPL fall_processing_complete
    LDX harry_board_x
    LDY harry_board_y
    DEY
    JSR cos_board_read_with_range_check
    AND #&01
    BEQ fall_processing_complete
    LDA #&00
    STA harry_state
    LDA #&00
    SEC
    SBC harry_cell_y
    STA harry_dy
    JMP fall_processing_complete
    .test_fall_landing_on_wall
    LDX harry_board_x
    LDY harry_board_y
    DEY
    JSR cos_board_read_with_range_check
    AND #&01
    BEQ fall_processing_complete
    LDA #&00
    STA harry_state
    .fall_processing_complete
    JMP harry_apply_dx_dy
    .state_four_on_lift
    LDA key_press_bits
    AND #&10
    BEQ not_jump_from_lift
    JMP start_jump
    .not_jump_from_lift
    LDA lift_x
    SEC
    SBC #&01
    CMP harry_x
    BCS fall_from_lift
    ADC #&0A
    CMP harry_x
    BCS not_fall_from_lift
    .fall_from_lift
    LDA #&00
    STA harry_fall_scaled_vy
    STA harry_fall_facing
    LDA #&03
    STA harry_state
    .not_fall_from_lift
    LDA #&01
    STA harry_dy
    LDA harry_dx
    BEQ not_moving_retain_facing
    STA harry_facing
    .not_moving_retain_facing
    JSR test_horizontal_obstructions
    BCC horizontal_movement_clear
    LDA #&00
    STA harry_dx
    .horizontal_movement_clear
    LDA harry_y
    CMP #&DC
    BCC harry_apply_dx_dy
    INC dead_flag
    .harry_apply_dx_dy
    LDA harry_sprite_index
    JSR draw_harry
    LDA harry_x
    CLC
    ADC harry_dx
    STA harry_x
    LDA harry_cell_x
    CLC
    ADC harry_dx
    BPL not_move_to_left_cell
    DEC harry_board_x
    .not_move_to_left_cell
    CMP #&08
    BMI not_move_to_right_cell
    INC harry_board_x
    .not_move_to_right_cell
    AND #&07
    STA harry_cell_x
    LDA harry_y
    CLC
    ADC harry_dy
    STA harry_y
    LDA harry_cell_y
    CLC
    ADC harry_dy
    BPL not_move_to_down_cell
    DEC harry_board_y
    .not_move_to_down_cell
    CMP #&08
    BMI not_move_to_up_cell
    INC harry_board_y
    .not_move_to_up_cell
    AND #&07
    STA harry_cell_y
    LDX #&06
    LDA harry_facing
    BEQ harry_facing_ladder
    BPL harry_not_facing_left
    LDX #&09
    .harry_not_facing_left
    STX param
    LDA harry_cell_x
    LSR A
    JMP harry_calculate_frame
    .harry_facing_ladder
    LDA #&0C
    STA param
    LDA harry_cell_y
    LSR A
    .harry_calculate_frame
    LDX #&02
    STX &89
    BIT &89
    BEQ zero_one_else_zero_two
    AND #&01
    ASL A
    .zero_one_else_zero_two
    LDX harry_state
    CPX #&01
    BNE harry_not_on_ladder
    LDX harry_dy
    BNE harry_apply_frame_offset
    LDA #&00
    JMP harry_apply_frame_offset
    .harry_not_on_ladder
    LDX harry_dx
    BNE harry_apply_frame_offset
    LDA #&00
    .harry_apply_frame_offset
    CLC
    ADC param
    STA harry_sprite_index
    JSR draw_harry
    LDX harry_board_x
    LDY harry_board_y
    LDA harry_cell_y
    CMP #&04
    BCC harry_in_bottom_half_of_cell
    INY
    .harry_in_bottom_half_of_cell
    STY &89
    JSR cos_board_read_with_range_check
    STA param
    AND #&0C
    BEQ rts
    AND #&08
    BNE consume_grain
    DEC egg_remaining_count
    LDA #&06
    STA sound3_pitch
    LXY sound3
    LDA #osword_7_play_sound
    JSR osword
    LDA param
    LSR A
    LSR A
    LSR A
    LSR A
    CLC
    ADC player_data_index
    TAX
    DEC player_egg_flags,X
    LDX harry_board_x
    LDY &89
    JSR delete_egg
    LDA level_number
    LSR A
    LSR A
    CLC
    ADC #&01
    CMP #&0A
    BCC egg_bonus_in_range
    LDA #&0A
    .egg_bonus_in_range
    LDX #&05
    JSR cos_add_to_score
    JMP rts
    .consume_grain
    LDA #&05
    STA sound3_pitch
    LXY sound3
    LDA #osword_7_play_sound
    JSR osword
    LDA param
    LSR A
    LSR A
    LSR A
    LSR A
    CLC
    ADC player_data_index
    TAX
    DEC player_grain_flags,X
    LDX harry_board_x
    LDY &89
    JSR delete_grain
    LDA #&05
    LDX #&06
    JSR cos_add_to_score
    LDA #&14
    STA clock_stop_timer
    .rts
    RTS
    .test_horizontal_obstructions
    LDA harry_dx
    BMI test_left
    BNE test_right
    CLC
    RTS
    .test_left
    LDA harry_x
    CMP #&01
    BCC horizontal_obstruction
    LDA harry_cell_x
    CMP #&02
    BCS no_obstruction
    LDA harry_dy
    CMP #&02
    BEQ no_obstruction
    LDX harry_board_x
    DEX
    LDY harry_board_y
    LDA harry_cell_y
    CLC
    ADC harry_dy
    CMP #&08
    BCC test_this_row_left
    BPL test_row_above_left
    DEY
    JMP test_this_row_left
    .test_row_above_left
    INY
    .test_this_row_left
    JSR cos_board_read_with_range_check
    CMP #&01
    BEQ horizontal_obstruction
    LDA harry_dy
    BPL no_obstruction
    LDX harry_board_x
    DEX
    INY
    JSR cos_board_read_with_range_check
    CMP #&01
    BEQ horizontal_obstruction
    BNE no_obstruction
    .test_right
    LDA harry_x
    CMP #&98
    BCS horizontal_obstruction
    LDA harry_cell_x
    CMP #&05
    BCC no_obstruction
    LDA harry_dy
    CMP #&02
    BEQ no_obstruction
    LDX harry_board_x
    INX
    LDY harry_board_y
    LDA harry_cell_y
    CLC
    ADC harry_dy
    CMP #&08
    BCC test_this_row_right
    BPL test_row_above_right
    DEY
    JMP test_this_row_right
    .test_row_above_right
    INY
    .test_this_row_right
    JSR cos_board_read_with_range_check
    CMP #&01
    BEQ horizontal_obstruction
    LDA harry_dy
    BPL no_obstruction
    LDX harry_board_x
    INX
    INY
    JSR cos_board_read_with_range_check
    CMP #&01
    BEQ horizontal_obstruction
    .no_obstruction
    CLC
    RTS
    .horizontal_obstruction
    SEC
    RTS
    .delete_egg
    TXA
    PHA
    LDA #&00
    JSR board_write
    LDA #&02
    STA sprite_colour_mask_left
    PLA
    TAX
    LDA #&03
    JSR cos_draw_sprite_at_text_coords
    RTS
}

.delete_grain
{
    TXA
    PHA
    LDA #&00
    JSR board_write
    LDA #&08
    STA sprite_colour_mask_left
    PLA
    TAX
    LDA #&04
    JSR cos_draw_sprite_at_text_coords
    RTS
}

.draw_harry
{
    LDX #&20
    STX sprite_colour_mask_left
    JSR cos_lookup_sprite
    LDX harry_x
    LDY harry_y
    JSR cos_calculate_screen_location
    JSR cos_draw_sprite
    RTS
}

.draw_duck
{
    LDX #&20
    STX sprite_colour_mask_left
    CLC
    ADC #&0F
    JSR cos_lookup_sprite
    LDX duck_x
    LDY duck_y
    JSR cos_calculate_screen_location
    JSR cos_draw_sprite
    RTS
}

.draw_chicken
{
    LDX #&80
    STX sprite_colour_mask_left
    LDX param
    LDA chicken_sprite_indices,X
    CLC
    ADC #&15
    PHA
    JSR cos_lookup_sprite
    LDX param
    LDA chicken_xs,X
    LDY chicken_ys,X
    TAX
    PLA
    CMP #&1D
    BCC not_feeding_left
    TXA
    SBC #&08
    TAX
    .not_feeding_left
    JSR cos_calculate_screen_location
    JSR cos_draw_sprite
    RTS
}

.update_lift
{
    LDA lift_flag
    BEQ no_lift
    LDY lift_y1
    LDA lift_alternator
    BEQ update_lift_1
    LDY lift_y2
    .update_lift_1
    STY &89
    LDA #&02
    STA sprite_colour_mask_left
    LDA #&05
    JSR cos_lookup_sprite
    LDX lift_x
    LDY &89
    JSR cos_calculate_screen_location
    JSR cos_draw_sprite
    INC &89
    INC &89
    LDA &89
    CMP #&E0
    BNE lift_not_at_top
    LDA #&06
    STA &89
    .lift_not_at_top
    LDA #&05
    JSR cos_lookup_sprite
    LDX lift_x
    LDY &89
    JSR cos_calculate_screen_location
    JSR cos_draw_sprite
    LDA lift_alternator
    BEQ write_lift_y1
    LDA &89
    STA lift_y2
    JMP written_lift_y
    .write_lift_y1
    LDA &89
    STA lift_y1
    .written_lift_y
    LDA lift_alternator
    EOR #&FF
    STA lift_alternator
    .no_lift
    RTS
}

.test_keys
{
    LDX #scan_H EOR &FF
    LDY #&FF
    LDA #osbyte_81_inkey
    JSR osbyte
    CPY #&00
    BEQ test_player_keys
    .pause_loop
    LDX #scan_H EOR &FF
    LDY #&FF
    LDA #osbyte_81_inkey
    JSR osbyte
    CPY #&00
    BEQ test_pause_over
    LDX #scan_ESCAPE EOR &FF
    LDY #&FF
    LDA #osbyte_81_inkey
    JSR osbyte
    CPY #&00
    BEQ test_pause_over
    LDA #&80
    STA key_press_bits
    RTS
    .test_pause_over
    JSR test_player_keys
    LDA key_press_bits
    BEQ pause_loop
    .test_player_keys
    LDA #&00
    STA key_press_bits
    LDA #&01
    STA &80
    LDX key_right
    JSR test_next_key
    LDX key_left
    JSR test_next_key
    LDX key_down
    JSR test_next_key
    LDX key_up
    JSR test_next_key
    LDX key_jump
    JSR test_next_key
    RTS
    .test_next_key
    LDY #&FF
    LDA #osbyte_81_inkey
    JSR osbyte
    CPY #&00
    BEQ not_pressed
    LDA key_press_bits
    ORA &80
    STA key_press_bits
    .not_pressed
    ASL &80
    RTS
}

.play_start {
    .game_loop
    JSR test_keys
    JSR update_harry
    JSR harry_motion_noises
    JSR update_lift
    JSR update_birds_timers
    JSR check_extra_lives
    JSR hit_test_birds
    JSR wait_for_interval_timer
    LDA dead_flag
    BNE harry_died
    LDA harry_y
    CMP #&11
    BCC harry_died
    LDA egg_remaining_count
    BEQ eggs_all_collected
    LDA key_press_bits
    BMI abort_game
    JMP game_loop

    .abort_game
    RUN_PROG "B.Home", 0

    .harry_died
    LXY dead_tune
    JSR play_tune
    RUN_PROG "B.Game", 2

    .eggs_all_collected
    RUN_PROG "B.Game", 3

    .check_extra_lives
    LDA extra_life_flag
    BNE extra_life
    RTS
    .extra_life
    LDA #&00
    STA extra_life_flag
    JSR draw_current_life_marker
    LDX player
    INC player_lives,X
    RTS

    .draw_current_life_marker
    LDA #&20
    STA sprite_colour_mask_left
    LDX player
    LDA player_lives,X
    CMP #&09
    BCC marker_visible
    RTS
    .marker_visible
    ASL A
    ASL A
    ADC score_x_position
    ADC #&0A
    TAX
    LDY #&EE
    JSR cos_calculate_screen_location
    LDA #&2F
    JSR cos_lookup_sprite
    JSR cos_draw_sprite
    RTS

    .play_tune
    STX sprite_lo
    STY sprite_hi
    LDY #&00
    STY &89
    LDA (sprite_lo),Y
    STA loop_counter
    .play_tune_loop
    LDY &89
    INY
    LDA (sprite_lo),Y
    STA sound2_pitch
    INY
    LDA (sprite_lo),Y
    STA sound2_duration
    STY &89
    LXY sound2
    LDA #osword_7_play_sound
    JSR osword
    DEC loop_counter
    BNE play_tune_loop
    RTS
    .dead_tune
    EQUB &10
    EQUS "!"
    EQUB &04
    EQUS ")"
    EQUB &02
    EQUS "!"
    EQUB &04
    EQUB &19
    EQUB &02
    EQUB &15
    EQUB &04
    EQUB &05
    EQUB &02
    EQUB &0D
    EQUB &04
    EQUB &01
    EQUB &02
    EQUB &05
    EQUB &0C
    EQUB &05
    EQUB &01
    EQUB &0D
    EQUB &01
    EQUB &15
    EQUB &01
    EQUB &19
    EQUB &01
    EQUS "!"
    EQUB &01
    EQUS "1"
    EQUB &01
    EQUS "5"
    EQUB &01
}

.wait_for_interval_timer
{
    LDA #osword_3_read_interval_timer
    LXY interval_time
    JSR osword
    LDA &01
    BNE reset_timer
    LDA &00
    CMP #&03
    BCC wait_for_interval_timer
    .reset_timer
    LDA #&00
    STA &00
    STA &01
    LDA #osword_4_write_interval_timer
    LXY interval_time
    JSR osword
    RTS
}

.harry_motion_noises
{
    LDA harry_dx
    ORA harry_dy
    BNE harry_moving
    RTS
    .harry_moving
    LDA birds_timer_cycle
    AND #&01
    BEQ even_tick
    RTS
    .even_tick
    LDA harry_state
    BNE harry_not_on_ground
    LDA #&64
    JMP play_sound1
    .harry_not_on_ground
    CMP #&01
    BNE harry_not_on_ladder
    LDA #&96
    JMP play_sound1
    .harry_not_on_ladder
    CMP #&02
    BNE harry_not_jumping
    LDA harry_fall_scaled_vy
    CMP #&0B
    BCC harry_jumping_upwards
    LDA #&BE
    SEC
    SBC harry_fall_scaled_vy
    SBC harry_fall_scaled_vy
    JMP play_sound1
    .harry_jumping_upwards
    LDA #&96
    CLC
    ADC harry_fall_scaled_vy
    ADC harry_fall_scaled_vy
    JMP play_sound1
    .harry_not_jumping
    CMP #&03
    BNE harry_not_falling
    LDA #&6E
    SEC
    SBC harry_fall_scaled_vy
    SBC harry_fall_scaled_vy
    JMP play_sound1
    .harry_not_falling
    LDA harry_dx
    BNE harry_moving_on_lift
    RTS
    .harry_moving_on_lift
    LDA #&64
    .play_sound1
    STA sound1_pitch
    LXY sound1
    LDA #osword_7_play_sound
    JSR osword
    RTS
    .sound1
    EQUB &13
    EQUB &00
    EQUB &01
    EQUB &00
    .sound1_pitch
    EQUB &00
    EQUB &00
    EQUB &01
    EQUB &00
}

.update_birds_timers
{
    INC birds_timer_cycle
    LDA birds_timer_cycle
    CMP #&08
    BNE cycle_not_overflowed
    LDA #&00
    STA birds_timer_cycle
    JMP update_duck
    .cycle_not_overflowed
    CMP #&04
    BNE cycle_not_midway
    JMP update_timers
    .cycle_not_midway
    JMP update_chickens
    .update_duck
    LDA duck_direction_flap
    AND #&02
    STA &8B
    LDA duck_loose_flag
    BEQ duck_in_cage
    LDA duck_x
    CLC
    ADC #&04
    CMP harry_x
    BCS duck_right_of_harry
    INC duck_vx
    LDA duck_vx
    CMP #&06
    BMI vx_less_than_6
    DEC duck_vx
    .vx_less_than_6
    LDA #&00
    STA &8B
    JMP vx_adjusted
    .duck_right_of_harry
    DEC duck_vx
    LDA duck_vx
    CMP #&FB
    BPL vx_greater_or_equal_minus_5
    INC duck_vx
    .vx_greater_or_equal_minus_5
    LDA #&02
    STA &8B
    .vx_adjusted
    LDA harry_y
    CLC
    ADC #&04
    CMP duck_y
    BCC duck_above_harry
    INC duck_vy
    LDA duck_vy
    CMP #&06
    BMI vy_less_than_6
    DEC duck_vy
    .vy_less_than_6
    JMP vy_adjusted
    .duck_above_harry
    DEC duck_vy
    LDA duck_vy
    CMP #&FB
    BPL vy_adjusted
    INC duck_vy
    .vy_adjusted
    LDA duck_y
    CLC
    ADC duck_vy
    CMP #&28
    BCS duck_not_at_bottom
    LDA duck_vy
    EOR #&FF
    STA duck_vy
    INC duck_vy
    .duck_not_at_bottom
    LDA duck_x
    CLC
    ADC duck_vx
    CMP #&90
    BCC duck_in_cage
    LDA duck_vx
    EOR #&FF
    STA duck_vx
    INC duck_vx
    .duck_in_cage
    LDA duck_direction_flap
    JSR draw_duck
    LDA duck_x
    CLC
    ADC duck_vx
    STA duck_x
    LDA duck_y
    CLC
    ADC duck_vy
    STA duck_y
    LDA duck_direction_flap
    AND #&01
    EOR #&01
    ORA &8B
    STA duck_direction_flap
    JSR draw_duck
    RTS
    .update_chickens
    DEC current_chicken
    LDX current_chicken
    BPL current_chicken_not_wrapped
    LDX chicken_speed
    STX current_chicken
    .current_chicken_not_wrapped
    CPX chicken_count
    BCC current_chicken_live
    RTS
    .current_chicken_live
    STX param
    LDA chicken_states,X
    CMP #&01
    BNE state_not_one
    JMP chicken_state_one
    .state_not_one
    BCC chicken_state_zero
    JMP chicken_state_two_or_more
    .chicken_state_zero
    LDA chicken_board_xs,X
    STA &8B
    LDA chicken_board_ys,X
    STA &8C
    LDA #&00
    STA &8D
    LDX &8B
    LDY &8C
    DEX
    DEY
    JSR cos_board_read_with_range_check
    AND #&01
    BEQ no_wall_left
    STA &8D
    .no_wall_left
    LDX &8B
    LDY &8C
    INX
    DEY
    JSR cos_board_read_with_range_check
    AND #&01
    BEQ no_wall_right
    LDA #&02
    ORA &8D
    STA &8D
    .no_wall_right
    LDX &8B
    LDY &8C
    DEY
    JSR cos_board_read_with_range_check
    AND #&02
    BEQ no_ladder_down
    LDA #&08
    ORA &8D
    STA &8D
    .no_ladder_down
    LDX &8B
    LDY &8C
    INY
    INY
    JSR cos_board_read_with_range_check
    AND #&02
    BEQ no_ladder_up
    LDA #&04
    ORA &8D
    STA &8D
    .no_ladder_up
    JSR count_chicken_options
    CPX #&01
    BNE multiple_options
    LDA &8D
    LDX param
    STA chicken_directions,X
    JMP one_option_selected
    .multiple_options
    LDX param
    LDA chicken_directions,X
    CMP #&04
    BCS using_ladder
    EOR #&FC
    JMP clear_opposite_direction_retain_orthogonal
    .using_ladder
    EOR #&F3
    .clear_opposite_direction_retain_orthogonal
    AND &8D
    STA &8D
    JSR count_chicken_options
    CPX #&01
    BNE multiple_options_still
    LDX param
    LDA &8D
    STA chicken_directions,X
    JMP one_option_selected
    .multiple_options_still
    LDA &8D
    STA &8E
    .choose_random_direction_loop
    JSR randomise
    LDA rand1
    AND &8E
    STA &8D
    JSR count_chicken_options
    CPX #&01
    BNE choose_random_direction_loop
    LDX param
    LDA &8D
    STA chicken_directions,X
    .one_option_selected
    LDX param
    LDA chicken_directions,X
    AND #&03
    BEQ chicken_state_one
    AND #&01
    BEQ going_right
    LDX &8B
    LDY &8C
    DEX
    JSR cos_board_read_with_range_check
    JMP test_grain
    .going_right
    LDX &8B
    LDY &8C
    INX
    JSR cos_board_read_with_range_check
    .test_grain
    AND #&08
    BEQ chicken_state_one
    LDX param
    LDA #&02
    STA chicken_states,X
    JMP chicken_state_one
    .count_chicken_options
    LDX #&00
    LDA &8D
    .count_bits_loop
    LSR A
    BCC bit_not_set
    INX
    .bit_not_set
    CMP #&00
    BNE count_bits_loop
    RTS
    .chicken_state_two_or_more
    CMP #&04
    BNE chicken_state_one
    LDA chicken_directions,X
    LDY chicken_board_xs,X
    STY &8B
    LDY chicken_board_ys,X
    STY &8C
    LDX &8B
    DEX
    AND #&01
    BNE moving_left
    INX
    INX
    .moving_left
    STX &8D
    JSR cos_board_read_with_range_check
    STA &89
    AND #&08
    BEQ chicken_state_one
    LDA &89
    LSR A
    LSR A
    LSR A
    LSR A
    CLC
    ADC player_data_index
    TAX
    DEC player_grain_flags,X
    LDX &8D
    LDY &8C
    JSR delete_grain
    .chicken_state_one
    JSR draw_chicken
    LDX param
    LDA chicken_states,X
    CMP #&02
    BCS chicken_eating
    LDA chicken_directions,X
    LSR A
    BCS move_chicken_left
    LSR A
    BCS move_chicken_right
    LSR A
    BCS move_chicken_up
    LDA chicken_ys,X
    SEC
    SBC #&04
    STA chicken_ys,X
    LDA chicken_states,X
    BEQ skip_dec_y
    DEC chicken_board_ys,X
    .skip_dec_y
    LDA #&04
    JMP move_chicken_finish
    .move_chicken_up
    LDA chicken_ys,X
    CLC
    ADC #&04
    STA chicken_ys,X
    LDA chicken_states,X
    BEQ skip_inc_y
    INC chicken_board_ys,X
    .skip_inc_y
    LDA #&04
    JMP move_chicken_finish
    .move_chicken_left
    LDA chicken_xs,X
    SEC
    SBC #&04
    STA chicken_xs,X
    LDA chicken_states,X
    BEQ skip_dec_x
    DEC chicken_board_xs,X
    .skip_dec_x
    LDA #&02
    JMP move_chicken_finish
    .move_chicken_right
    LDA chicken_xs,X
    CLC
    ADC #&04
    STA chicken_xs,X
    LDA chicken_states,X
    BEQ skip_inc_x
    INC chicken_board_xs,X
    .skip_inc_x
    LDA #&00
    JMP move_chicken_finish
    .move_chicken_finish
    STA chicken_sprite_indices,X
    LDA chicken_states,X
    EOR #&01
    STA chicken_states,X
    CLC
    ADC chicken_sprite_indices,X
    STA chicken_sprite_indices,X
    JSR draw_chicken
    RTS
    .chicken_eating
    LDA chicken_states,X
    ASL A
    AND #&1F
    STA chicken_states,X
    BEQ feeding_complete
    LDA #&06
    .feeding_complete
    LDY chicken_directions,X
    CPY #&01
    BNE moving_right
    CLC
    ADC #&02
    .moving_right
    LDY chicken_states,X
    CPY #&08
    BNE head_not_on_ground
    CLC
    ADC #&01
    .head_not_on_ground
    STA chicken_sprite_indices,X
    JSR draw_chicken
    RTS
    .update_timers
    LDA #&08
    STA sprite_colour_mask_left
    LDA clock_stop_timer
    BEQ clock_not_stopped
    DEC clock_stop_timer
    RTS
    .clock_not_stopped
    LDX #&02
    STX param
    .decrement_timer_loop
    JSR draw_timer_digit
    LDX param
    DEC timer_2,X
    PHP
    BPL no_borrow
    LDA #&09
    STA timer_2,X
    .no_borrow
    JSR draw_timer_digit
    DEC param
    PLP
    BMI decrement_timer_loop
    CLC
    LDA timer_2
    ADC timer_1
    ADC timer_0
    BNE time_remains
    INC dead_flag
    RTS
    .time_remains
    LDA timer_0
    BEQ five_divides_time
    CMP #&05
    BEQ five_divides_time
    RTS
    .five_divides_time
    LDA bonus_gone_flag
    BEQ decrement_bonus
    RTS
    .decrement_bonus
    JMP cos_decrement_bonus
}

.draw_timer_digit
{
    LDA param
    TAY
    ASL A
    ASL A
    ADC param
    ADC #&91
    TAX
    LDA timer_2,Y
    LDY #&E7
    JSR cos_draw_digit
    RTS
}

.hit_test_birds
{
    LDA chicken_count
    BEQ hit_test_duck
    LDA #&00
    STA loop_counter
    .hit_test_chicken_loop
    LDX loop_counter
    LDA chicken_xs,X
    SEC
    SBC harry_x
    CLC
    ADC #&05
    CMP #&0B
    BCS chicken_miss
    LDA chicken_ys,X
    SEC
    SBC #&01
    SBC harry_y
    CLC
    ADC #&0E
    CMP #&1D
    BCS chicken_miss
    INC dead_flag
    .chicken_miss
    INC loop_counter
    LDA loop_counter
    CMP chicken_count
    BCC hit_test_chicken_loop
    .hit_test_duck
    LDA duck_loose_flag
    BEQ duck_in_cage
    LDA duck_x
    CLC
    ADC #&04
    SEC
    SBC harry_x
    CLC
    ADC #&05
    CMP #&0B
    BCS duck_in_cage
    LDA duck_y
    SEC
    SBC #&05
    SBC harry_y
    CLC
    ADC #&0E
    CMP #&1D
    BCS duck_in_cage
    INC dead_flag
    .duck_in_cage
    RTS
}

.randomise
{
    LDA rand1
    AND #&48
    ADC #&38
    ASL A
    ASL A
    ROL rand4
    ROL rand3
    ROL rand2
    ROL rand1
    RTS
}

.sound2
EQUB &03
EQUB &00
EQUB &02
EQUB &00
.sound2_pitch
EQUS "x"
EQUB &00
.sound2_duration
EQUB &1E
EQUB &00
.sound3
EQUB &10
EQUB &00
EQUB &03
EQUB &00
.sound3_pitch
EQUB &00
EQUB &00
EQUB &04
EQUB &00

.play_prog_end
WRITE_MODULE "Play", mini_prog_start_address, play_prog_end, play_start

